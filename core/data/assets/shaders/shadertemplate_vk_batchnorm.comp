/* Copyright (C) 2020 - 2022 OPPO. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#version 450 core
#extension GL_EXT_control_flow_attributes : enable
#extension GL_EXT_shader_explicit_arithmetic_types_float16 : enable

#ifdef FP16_PRECISION
#define PRECISION mediump
precision PRECISION float;
#define OUTPUT_FORMAT rgba16f
#else
#define PRECISION highp
precision PRECISION float;
#define OUTPUT_FORMAT rgba32f
#endif

layout(local_size_x = WG_X, local_size_y = WG_Y, local_size_z = WG_Z) in;

layout(set=0, binding=0, rgba32f) writeonly uniform PRECISION image3D uOutput;
layout(set=0, binding=1) uniform PRECISION sampler3D uInput;

layout(set=0, binding=2) uniform constBuffer {
    ivec4 inputSize;
    int activationType;
    float leakyValue;
} uConstant;

layout(set=0, binding=3) readonly buffer beta{
    vec4 data[];
} uBeta;
layout(set=0, binding=4) readonly buffer gamma{
    vec4 data[];
} uGamma;
layout(set=0, binding=5) readonly buffer mean{
    vec4 data[];
} uMean;
layout(set=0, binding=6) readonly buffer variance{
    vec4 data[];
} uVariance;

void main()
{
    ivec3 pos = ivec3(gl_GlobalInvocationID);
    ivec3 inSize = uConstant.inputSize.xyz;
    int activationType = uConstant.activationType;
    float leakyValue = uConstant.leakyValue;
    if (all(lessThan(pos, inSize)))
    {
        vec4 color = texelFetch(uInput, pos.xyz, 0);
        vec4 movingVariance = uVariance.data[pos.z]; 
        vec4 movingMean = uMean.data[pos.z];         
        vec4 gamma = uGamma.data[pos.z]; 
        vec4 beta = uBeta.data[pos.z];                 
        vec4 sqrtVar = sqrt(movingVariance + vec4(0.001f)); 
        sqrtVar = max(sqrtVar, vec4(0.0001f)); 
        color = ((gamma/sqrtVar) * (color - movingMean)) + beta;  
        // color = beta;
        if (activationType == 0) {  //No activation
            imageStore(uOutput, pos, color);
            return;
        } 
        if (activationType == 1) {  //RELU
            color = max(color, vec4(0));
            imageStore(uOutput, pos, color);
            return;
        } 
        if (activationType == 2) { //RELU6
            color = clamp(color, vec4(0), vec4(6));
            imageStore(uOutput, pos, color);
            return;
        } 
        if (activationType == 3) { //TANH
            color = tanh(color);
            imageStore(uOutput, pos, color);
            return;
        } 
        if (activationType == 4) { //SIGMOID
            color  = vec4(1.0f)/(vec4(1.0f)+ exp(-color));
            imageStore(uOutput, pos, color);
            return;
        }         
        if (activationType == 5) { //LEAKYRELU
            color   = max(color,  (color * vec4(leakyValue)));
            imageStore(uOutput, pos, color);
            return;
        } 
        if (activationType == 6) {  //SILU
            color    = color  * vec4(1.0f)/(vec4(1.0f)+ exp(-color));
            imageStore(uOutput, pos, color);
            return;
        } 
    }
}

