/* Copyright (C) 2020 - 2022 OPPO. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#version 440 core
layout(std430) buffer;

#ifdef FP16_PRECISION
#define PRECISION mediump
precision PRECISION float;
#define OUTPUT_FORMAT rgba16f
#else
#define PRECISION highp
precision PRECISION float;
#define OUTPUT_FORMAT rgba32f
#endif

layout(set=0, binding=0, OUTPUT_FORMAT) writeonly restrict uniform PRECISION image3D uOutput;
layout(set=0, binding=1) uniform PRECISION sampler3D uInput;

layout(set=0, binding=2) uniform constBuffer {
    ivec4 inputSize;
    ivec4 outputSize;
    ivec2 pad;
    ivec2 kernelSize;
    ivec2 stride;
} uConstant;

layout(local_size_x = WG_X, local_size_y = WG_Y, local_size_z = WG_Z) in;

void main()
{
    ivec3 pos = ivec3(gl_GlobalInvocationID);
    ivec3 outputSize = uConstant.outputSize.xyz;
    ivec2 spos = pos.xy*uConstant.stride-uConstant.pad;
    int batch = pos.z / uConstant.outputSize.z;
    int depth = pos.z % uConstant.outputSize.z;

    if (all(lessThan(ivec4(pos.x, pos.y, depth, batch), uConstant.outputSize)))
    {
        ivec2 inputSizeXY = uConstant.inputSize.xy;
        vec4 color = vec4(-100000.0);
        ivec2 sfxy = max(ivec2(0), -spos);
        ivec2 efxy = min(uConstant.kernelSize, inputSizeXY-spos);

        for (int fy=sfxy.y; fy<efxy.y; ++fy)
        {
            for (int fx=sfxy.x; fx<efxy.x; ++fx)
            {
                ivec2 spos_ = spos + ivec2(fx, fy);
                //color = max(texelFetch(uInput, ivec3(spos_.x + depth * uConstant.inputSize.x, spos_.y + batch * uConstant.inputSize.y, pos.z), 0), color);
                color = max(color, texelFetch(uInput, ivec3(spos_.x, spos_.y, pos.z),0));
            }
        }
        //imageStore(uOutput, ivec3(pos.x + depth * uConstant.outputSize.x, pos.y + depth * uConstant.outputSize.y, pos.z), color);
        imageStore(uOutput, pos, color);
        //imageStore(uOutput, pos.xyz, vec4(1.0, 2.0, 3.0, pos.z));
    }
}